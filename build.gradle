group 'org.tuberlin.fak4'
version '1.2.1'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = 'main.Main'

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName,
        )
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

repositories {
    maven { url 'http://download.osgeo.org/webdav/geotools/' }
    mavenCentral()
}

dependencies {
    compile("org.icepdf.os:icepdf-core:6.2.2") {
        exclude group: "com.sun.media", module: 'jai_imageio'
        exclude group: "javax.media", module: "jai_core"
    }
    compile "org.icepdf.os:icepdf-viewer:6.2.2"
    compile 'com.levigo.jbig2:levigo-jbig2-imageio:2.0'


    compile group: 'com.1stleg', name: 'jnativehook', version: '2.1.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    compile 'commons-cli:commons-cli:1.4'

    compile 'org.bidib.jbidib.org.qbang.rxtx:rxtxcomm:2.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

configurations.all*.dependencies*.withType(ModuleDependency)*.each {
    it.exclude group: "com.sun.media", module: 'jai_imageio'
}